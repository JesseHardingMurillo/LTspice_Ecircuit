*Half wave circuit with Diode
*------------------------------------------------------------------------------------------------------------------------------------------
*Circuit
*------------------------------------------------------------------------------------------------------------------------------------------
VS	in	0	AC 		1 		SIN({p_offset} {p_ampl} {p_freq})	; Source
D1	in	out	1N4148
R1	out	0	{p_r1}
*------------------------------------------------------------------------------------------------------------------------------------------
*Models
*------------------------------------------------------------------------------------------------------------------------------------------
.model D D
*Standard library
.lib C:\Users\jesse\AppData\Local\LTspice\lib\cmp\standard.dio
*------------------------------------------------------------------------------------------------------------------------------------------
*Parameters: User input
*------------------------------------------------------------------------------------------------------------------------------------------
.param		p_r1			1k 				; Resistor Value
.param		p_n_cycles		10				; Number of cycles
.param		p_offset		0				; Offset
.param		p_ampl			1				; Amplitude
.param		p_freq			1k				; Frequency
*------------------------------------------------------------------------------------------------------------------------------------------
*Parameters: Automatic calculation
*------------------------------------------------------------------------------------------------------------------------------------------
.param		p_period		1/p_freq		; Period
.param		p_start_time	p_period*4	; Start, for measurement's purposes
.param		p_stop_time		p_period*6	; Stop, for measurement's purposes
*------------------------------------------------------------------------------------------------------------------------------------------
*Measurements
*------------------------------------------------------------------------------------------------------------------------------------------
.meas		TRAN	Vin_max			MAX		V(in)				From p_start_time to p_stop_time
.meas		TRAN	Vout_max		MAX		V(out)				From p_start_time to p_stop_time
.meas		TRAN	Diff_Vin_Vout	PARAM	Vin_max-Vout_max	; Vmax(in) - Vmax(out)
*------------------------------------------------------------------------------------------------------------------------------------------
* ANALYSIS
*------------------------------------------------------------------------------------------------------------------------------------------
.tran {p_period*p_n_cycles}
.PLOT
.PROBE
.END
