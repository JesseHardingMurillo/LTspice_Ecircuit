.SUBCKT NAND 1 2 3 4
* TERMINALS A B OUT VCC
RL	3	4	500
*Sx n1 n2   nc+ nc-   <model>
S1	3 	5	1 	0 		SW
S2	5 	0	2 	0 		SW
.ENDS
*

.SUBCKT NAND3 1 2 3 4 5
* TERMINALS   A B C OUT VCC
RL	4	5 	500
*Sx n1 n2   nc+ nc-   <model>
S1  4 	6 	1 0  SW
S2  6 	7 	2 0  SW
S3  7 	0 	3 0  SW
.ENDS
*
.SUBCKT AND 1 2 3 4
* TERMINALS A B OUT VCC
S1	4 5	1 0 	SW
S2	5 3	2 0 	SW
RL	3	0	500
*CL 3 0 10PF			; Delay
.ENDS
*
.SUBCKT AND_DELAY 1 2 3 4
* TERMINALS A B OUT VCC
S1	4 5	1 0 	SW
S2	5 3	2 0 	SW
RL	3	0	500
*CL 	3 	0 	1PF			; Delay
CL 	3 	0 	15PF			; Delay
.ENDS
*
.SUBCKT NOR 1 2 3 4
* TERMINALS A B OUT VCC
RL	3	4	500
S1	3 0	1 0 	SW
S2	3 0	2 0 	SW
.ENDS
*
.SUBCKT NOT 1 3 4
* TERMINALS A OUT VCC
RL	3	4	500
S1	3 0	1 0 	SW
.ENDS
*
.SUBCKT OR 1 2 3 4
* TERMINALS A B OUT VCC
RL	3	0	500
*Sx n1 n2 nc+ nc- <model>
S1	4 3	1 0 	SW
S2	4 3	2 0 	SW
.ENDS

* XOR using NAND
.SUBCKT XOR 1 2 3 4
* TERMINALESs A B OUT VCC
XNAND1 1 2 	5 4 NAND  ; NAND de A y B
XNAND2 1 5 	6 4 NAND  ; NAND de A y B
XNAND3 2 5 	7 4 NAND  ; NAND de A y B
XNAND4 6 7 	3 4 NAND  ; NAND de A y B
.ENDS

.MODEL	SW	VSWITCH(VON=2.6 VOFF=2.4 RON=5 ROFF=1MEG)
*
